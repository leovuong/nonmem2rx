$PROB Urine DAta problem (urine2.ctl)

$INPUT ID TIME AMT RATE DV MDV EVID CMT UVOL

$DATA urine.dat IGNORE=@

$SUBROUTINE ADVAN13 TOL=6

$MODEL NCOMPARTMENTS=1

$PK
MU_1=THETA(1)
MU_2=THETA(2)
MU_3=THETA(3)
CL=EXP(MU_1+ETA(1))
V=EXP(MU_2+ETA(2))
CLR=EXP(MU_3+ETA(3))
F2=CLR/CL

$DES
DADT(1)=-CL/V*A(1)

$ERROR
CP=A(1)/V
UVL=UVOL
IF(UVL<=0.0) UVL=1.0
CU=A(2)/UVL

IF(CMT==1) THEN
IPRE=CP
W=IPRE+1.0E-10
Y=IPRE + W*EPS(1)
ELSE
IPRE=CU
W=IPRE+1.0E-10
Y=IPRE + W*EPS(2)
ENDIF

$THETA 
2 ; [LN(CL)]
4 ; [LN(V)]
1 ; [LN(CLR)]

$OMEGA BLOCK(3) VALUES(0.1,0.001) ;[P]

$SIGMA 
0.1 ;[P]
0.1 ;[P]

$EST METHOD=IMP PRINT=1 AUTO=1 SIGL=6
$COV UNCONDITIONAL MATRIX=R PRINT=E
$TABLE ID TIME AMT RATE DV IPRE MDV EVID CMT UVOL CL V CLR NOAPPEND ONEHEADER NOPRINT FILE=urine2.tab
