;Model Desc: Full model
;Project Name: chapter6
;Project ID: NO PROJECT DESCRIPTION

$PROB 
$INPUT C ID TIME DV AMT RATE WT AGE SEX
$DATA 501.csv IGNORE=C
$SUBROUTINE ADVAN1 TRANS2
$PK 
include \nonmem\nonmem_reserved_general
MUFIRSTREC=1
OBJQUICK=1
  LTVCL=THETA(1)+LOG(WT/70)*THETA(3)+LOG(AGE/50)*THETA(5)+SEX*THETA(7)
  LTVV=THETA(2)+LOG(WT/70)*THETA(4)+LOG(AGE/50)*THETA(6)+SEX*THETA(8)
  MU_1=LTVCL
  MU_2=LTVV
  CL=EXP(MU_1+ETA(1))
  V=EXP(MU_2+ETA(2))
  S1=V
$THETA
  0.7  ;[LCL]
  3.0 ;[LV]
  0.8    ;[CL~WT]
  0.8    ;[V~WT]
  -0.1   ;[CL~AGE]
  0.1    ;[V~AGE]
  0.7    ;[CL~SEX]
  0.7    ;[V~SEX]

$ERROR
  IPRE=A(1)/V
  Y=IPRE*(1+EPS(1))

$OMEGA BLOCK(2)
  0.1 ;[p]
  0.001 ;[f]
  0.1 ;[p]
$SIGMA 
  0.04 ;[p]

$PRIOR NWPRI
$THETAP (0.01 FIXED)X8
$THETAPV BLOCK(8) VALUES(100000.0,0.0) FIXED 
$OMEGAP  BLOCK(2) VALUES(0.04,0.0) FIXED
$OMEGAPD (2 FIXED)
$SIGMAP BLOCK(1) (0.05 FIXED)
$SIGMAPD (1 FIXED)

$EST METHOD=NUTS AUTO=1 PRINT=50 NITER=2000
$COV UNCONDITIONAL MATRIX=R PRINT=E
; Unless FNLETA=0, table items are evaluated at mode of posterior of each subject
$TABLE ID TIME DV IPRE CL V ETA1 ETA2 NOPRINT NOAPPEND ONEHEADER FORMAT=,1PE13.6 FILE=504_nuts.tab
