$PROB RUN# example6 (from r2compl)
$INPUT C SET ID JID TIME DV=CONC DOSE=AMT RATE EVID MDV CMT TSTRAT TMIN TMAX DSTRAT DMIN DMAX
$DATA optex6d17.csv IGNORE=C

$SUBROUTINES ADVAN13 TRANS1 TOL=10 ATOL=10
$MODEL NCOMPARTMENTS=3

$PK
MU_1=THETA(1)
MU_2=THETA(2)
MU_3=THETA(3)
MU_4=THETA(4)
MU_5=THETA(5)
MU_6=THETA(6)
MU_7=THETA(7)
MU_8=THETA(8)
VC=EXP(MU_1+ETA(1))
K10=EXP(MU_2+ETA(2))
K12=EXP(MU_3+ETA(3))
K21=EXP(MU_4+ETA(4))
VM=EXP(MU_5+ETA(5))
KMC=EXP(MU_6+ETA(6))
K03=EXP(MU_7+ETA(7))
K30=EXP(MU_8+ETA(8))
S3=VC
S1=VC
KM=KMC*S1
F3=K03/K30

$DES
DADT(1) = -(K10+K12)*A(1) + K21*A(2) - VM*A(1)*A(3)/(A(1)+KM)
DADT(2) = K12*A(1) - K21*A(2)
DADT(3) =  -(VM-K30)*A(1)*A(3)/(A(1)+KM) - K30*A(3) + K03

$ERROR
ETYPE=1
IF(CMT.NE.1) ETYPE=0
IPRED=F
Y = F + ETYPE*( F*EPS(1)+EPS(2) ) + (1.0-ETYPE)*(F*EPS(3)+EPS(4))


$THETA 
3.90834E+00 
-2.18787E+00  
5.57985E-01 
-1.86377E-01  
2.26146E+00  
2.10476E-01  
3.70795E+00 
-7.08909E-01 

$OMEGA (0.0625 FIXED)X8
$SIGMA  (9.27944E-03 FIXED) (0.0001 FIXED) (2.24692E-02 FIXED) (0.0001 FIXED)

; Repeat $DESIGN optimization times, to assure Nelder has reached Minimum
$DESIGN    OFVTYPE=8 
;  design element TIME is sought to be otpimized
           DESEL=TIME DESELSTRAT=TSTRAT DESELMIN=TMIN DESELMAX=TMAX
;  design element DOSE (AMT) is also sought to be otpimized
           DESEL=DOSE DESELSTRAT=DSTRAT DESELMIN=DMIN DESELMAX=DMAX
           MAXEVAL=1000 SIGL=12 nohabort PRINT=100 FIMDIAG=1

$DESIGN    OFVTYPE=8 DESEL=TIME 
           DESELSTRAT=TSTRAT DESELMIN=TMIN DESELMAX=TMAX
           DESEL=DOSE DESELSTRAT=DSTRAT DESELMIN=DMIN DESELMAX=DMAX
           MAXEVAL=1000 SIGL=12 nohabort PRINT=100

$DESIGN    OFVTYPE=8 DESEL=TIME 
           DESELSTRAT=TSTRAT DESELMIN=TMIN DESELMAX=TMAX
           DESEL=DOSE DESELSTRAT=DSTRAT DESELMIN=DMIN DESELMAX=DMAX
           MAXEVAL=1000 SIGL=12 nohabort PRINT=100

$DESIGN    OFVTYPE=8 DESEL=TIME 
           DESELSTRAT=TSTRAT DESELMIN=TMIN DESELMAX=TMAX
           DESEL=DOSE DESELSTRAT=DSTRAT DESELMIN=DMIN DESELMAX=DMAX
           MAXEVAL=1000 SIGL=12 nohabort PRINT=100

$DESIGN    OFVTYPE=8 DESEL=TIME 
           DESELSTRAT=TSTRAT DESELMIN=TMIN DESELMAX=TMAX
           DESEL=DOSE DESELSTRAT=DSTRAT DESELMIN=DMIN DESELMAX=DMAX
           MAXEVAL=1000 SIGL=12 nohabort PRINT=100

$DESIGN    OFVTYPE=8 DESEL=TIME 
           DESELSTRAT=TSTRAT DESELMIN=TMIN DESELMAX=TMAX
           DESEL=DOSE DESELSTRAT=DSTRAT DESELMIN=DMIN DESELMAX=DMAX
           MAXEVAL=1000 SIGL=12 nohabort PRINT=100

; Report the final times and dose in the user defined table:
$TABLE ID DOSE CMT TIME EVID MDV DV TSTRAT NOPRINT NOAPPEND FILE=optex6d17_8.tab 
